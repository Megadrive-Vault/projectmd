;----------------------------------------------------------------------------
; Project MD
; Copyright 2012 Javier Degirolmo
;
; This file is part of Project MD.
;
; Project MD is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; Project MD is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Project MD.  If not, see <http://www.gnu.org/licenses/>.
;----------------------------------------------------------------------------

;----------------------------------------------------------------------------

;--------------------------------------------------------------------------;
; VarSrcDestDMA                                                            ;
;                                                                          ;
; Performs a DMA transfer operation from ROM/RAM to VRAM                   ;
; The operation is stored in the VDP command buffer                        ;
; This macro is meant to be used when length has a constant value, but     ;
; source and destination may change                                        ;
;--------------------------------------------------------------------------;
; INPUT                                                                    ;
;   d6.l ... Source address (ROM/RAM)                                      ;
;   d7.w ... Destination address (VRAM)                                    ;
;   len .... Length (in bytes)                                             ;
;--------------------------------------------------------------------------;
; BREAKS: d6, d7, a6                                                       ;
;--------------------------------------------------------------------------;

VarSrcDestDMA macro len
    lsr.l   #1, d6
    move.l  (RAM_VDPBufPos), a6
    move.l  #$97009600, 4(a6)
    move.w  #$9500, 8(a6)
    movep.l d6, 3(a6)
    move.l  #$94009300|(((len)<<7)&$7F0000)|(((len)>>1)&$FF), (a6)
    add.l   #10, a6
    andi.l  #$FFFF, d7
    add.l   d7, d7
    add.l   d7, d7
    lsr.w   #2, d7
    swap    d7
    or.l    #$40000080, d7
    move.l  d7, (a6)+
    move.l  a6, (RAM_VDPBufPos)
    endm
