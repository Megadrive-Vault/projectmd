;----------------------------------------------------------------------------
; Project MD
; Copyright 2012 Javier Degirolmo
;
; This file is part of Project MD.
;
; Project MD is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; Project MD is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Project MD.  If not, see <http://www.gnu.org/licenses/>.
;----------------------------------------------------------------------------

;----------------------------------------------------------------------------

;-------------------------------------------;
; RunWLIceFloor                             ;
;                                           ;
; Game logic code for waterlava's ice floor ;
;-------------------------------------------;

RunWLIceFloor:

;-------------------;
; Are we outbounds? ;
;-------------------;

    move.w  Object_X(a0), d0
    sub.w   (RAM_Scroll), d0
    cmp.w   #0-$40, d0
    blt.s   @Outbounds
    cmp.w   #320+$40, d0
    ble.s   @NotOutbounds
@Outbounds:
    bra     DeleteObject
@NotOutbounds:

;----------------------------;
; Are we in the water world? ;
;----------------------------;

    btst.b  #IngameStat_World, (RAM_IngameStatus+1)
    bne.s   @WaterWorld
    rts
@WaterWorld:

;-------------------------------;
; Is the player stepping on us? ;
;-------------------------------;

@CheckCol:
    move.w  Player_Gravity(a3), d0
    blt.s   @End

    move.w  Object_X(a0), d0
    move.w  Player_X(a3), d1
    addq.w  #3, d1
    cmp.w   d0, d1
    blt.s   @End

    add.w   #32, d0
    subq.w  #7, d1
    cmp.w   d0, d1
    bgt.s   @End

    move.w  Object_Y(a0), d0
    move.w  Player_Y(a3), d1
    subq.w  #1, d0
    cmp.w   d0, d1
    blt.s   @End

    addq.w  #8, d0
    cmp.w   d0, d1
    bgt.s   @End

;---------------------------;
; Make Stephany ride on us! ;
;---------------------------;

    subq.w  #8, d0
    move.w  d0, Player_Y(a3)
    move.w  #0, Player_Y+2(a3)

    move.w  Player_Status(a3), d0
    bset.l  #PlayerStat_Ride, d0
    move.w  d0, Player_Status(a3)
    move.w  #0, Player_Gravity(a3)
    move.l  #0, Player_RideSpeed(a3)
    move.l  #0, Player_RideGrav(a3)

;-----------------;
; Oops! Slippery! ;
;-----------------;

    move.l  Player_Speed(a3), d0
    move.l  d0, d1
    asr.l   #3, d1
    add.l   d1, d0
    move.l  d0, Player_Speed(a3)

;-------------------;
; End of subroutine ;
;-------------------;

@End:
    rts

;----------------------------------------------------------------------------
