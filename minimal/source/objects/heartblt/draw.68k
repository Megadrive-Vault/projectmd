;----------------------------------------------------------------------------
; Project MD
; Copyright 2012 Javier Degirolmo
;
; This file is part of Project MD.
;
; Project MD is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; Project MD is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Project MD.  If not, see <http://www.gnu.org/licenses/>.
;----------------------------------------------------------------------------

;----------------------------------------------------------------------------

;-----------------------------;
; DrawHeartBullet             ;
;                             ;
; Draws a heart bullet object ;
;-----------------------------;

DrawHeartBullet:

;-------------------------------------------------------------------------;
; Check if we're visible for starters                                     ;
; Spurious sprites cause sprite overflow                                  ;
; Also the sprite may appear at X coordinate 0, which accidentally causes ;
; the sprite cut feature to kick in...                                    ;
;-------------------------------------------------------------------------;

    move.w  Object_X(a0), d0
    sub.w   (RAM_Scroll), d0

    cmp.w   #-8, d0
    ble.s   @End
    cmp.w   #320, d0
    bge.s   @End

;------------------------------;
; Calculate sprite coordinates ;
;------------------------------;

    move.w  Object_Y(a0), d1
    add.w   #128, d0
    add.w   #128, d1

;----------------------------------------------;
; Give the heart path a wavy shape             ;
; Looks nicer than just going straight forward ;
;----------------------------------------------;

    lea     (Sines), a6
    move.w  (RAM_GlobalAnim), d7
    lsl.w   #4, d7
    addq.w  #2, d7
    and.w   #$FF<<1, d7
    move.w  (a6,d7.w), d7
    asr.w   #8, d7
    asr.w   #3, d7
    add.w   d7, d1

;-------------------------;
; Determine sprite to use ;
;-------------------------;

    move.w  #$8000|VRAM_Player_Heart, d2
    cmp.b   #Level_Light, (RAM_Level)
    bne.s   @NoDarken
    bclr.l  #15, d2
@NoDarken:

;----------------------;
; Put sprite on screen ;
;----------------------;

    moveq   #%0000, d3
    bsr     AddSprite

;--------------------------------;
; Draw mirror in 3D-themed level ;
;--------------------------------;

    move.b  (RAM_Level), d7
    cmp.b   #Level_3D, d7
    bne.s   @NoMirror

    move.w  Object_Y(a0), d1
    sub.w   #192-8, d1
    neg.w   d1
    add.w   #192+128, d1

    cmp.w   #128+192, d1
    blt.s   @NoMirror
    cmp.w   #128+224, d1
    bge.s   @NoMirror

    eor.w   #$B000, d2
    bsr     AddSprite
@NoMirror:

;-------------------;
; End of subroutine ;
;-------------------;

@End:
    rts

;----------------------------------------------------------------------------
