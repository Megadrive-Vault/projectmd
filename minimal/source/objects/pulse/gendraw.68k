;----------------------------------------------------------------------------
; Project MD
; Copyright 2012 Javier Degirolmo
;
; This file is part of Project MD.
;
; Project MD is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; Project MD is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Project MD.  If not, see <http://www.gnu.org/licenses/>.
;----------------------------------------------------------------------------

;----------------------------------------------------------------------------

;--------------------------------;
; DrawPulseGenerator             ;
;                                ;
; Draws a pulse generator object ;
;--------------------------------;

DrawPulseGenerator:

;----------------------------------------;
; Check if we're visible for starters    ;
; Spurious sprites cause sprite overflow ;
;----------------------------------------;

    move.w  Object_X(a0), d0
    sub.w   (RAM_Scroll), d0

    cmp.w   #-48, d0
    ble.s   @End
    cmp.w   #320, d0
    bge.s   @End

;-----------------------------------------;
; Check which sprite index to use, if any ;
;-----------------------------------------;

    move.w  Object_Misc(a0), d4
    sub.w   #30-8, d4
    blt.s   @End
    bclr.l  #0, d4
    add.w   d4, d4
    add.w   d4, d4

    lea     (@Patterns), a6
    lea     (a6,d4.w), a6

;------------------------;
; Get sprite coordinates ;
;------------------------;

    move.w  Object_X(a0), d0
    move.w  Object_Y(a0), d1
    sub.w   (RAM_Scroll), d0
    add.w   (a6)+, d0
    add.w   (a6)+, d1

;----------------------;
; Add sprite on screen ;
;----------------------;

    move.w  (a6)+, d2
    move.w  (a6), d3
    bsr     AddSprite

;-------------------;
; End of subroutine ;
;-------------------;

@End:
    rts

;----------------------------------------------------------------------------

;-----------------------------------------------------;
; Patterns used to determine sprite position and size ;
;-----------------------------------------------------;

@Patterns:
    dc.w    128+16+12, 128+12, $8000|(VRAM_PulseGen), %0000
    dc.w    128+16+8, 128+8, $8000|(VRAM_PulseGen+1), %0101
    dc.w    128+16+4, 128+4, $8000|(VRAM_PulseGen+1+4), %1010
    dc.w    128+16, 128+8, $8000|(VRAM_PulseGen+1+4+9), %1101

;----------------------------------------------------------------------------
