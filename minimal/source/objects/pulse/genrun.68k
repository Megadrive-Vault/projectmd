;----------------------------------------------------------------------------
; Project MD
; Copyright 2012 Javier Degirolmo
;
; This file is part of Project MD.
;
; Project MD is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; Project MD is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Project MD.  If not, see <http://www.gnu.org/licenses/>.
;----------------------------------------------------------------------------

;----------------------------------------------------------------------------

;---------------------------------------------;
; RunPulseGenerator                           ;
;                                             ;
; Game logic code for pulse generator objects ;
;---------------------------------------------;

RunPulseGenerator:

;-------------------;
; Are we outbounds? ;
;-------------------;

    move.w  Object_X(a0), d0
    sub.w   (RAM_Scroll), d0
    cmp.w   #0-$80, d0
    blt.s   @Outbounds
    cmp.w   #320+$40, d0
    ble.s   @Continue

@Outbounds:
    bra     DeleteObject

;------------------;
; Increase counter ;
;------------------;

@Continue:
    move.l  (RAM_GlobalAnimSpd), d0
    move.l  Object_Misc(a0), d1
    add.l   d0, d1

;-------------------;
; Create new pulse? ;
;-------------------;

    cmp.l   #30<<16, d1
    blt.s   @NoNewPulse
    sub.l   #30<<16, d1

    move.w  #Obj_Pulse, Object_Type(a1)
    move.l  Object_X(a0), Object_X(a1)
    move.l  Object_Y(a0), Object_Y(a1)
    move.l  #0, Object_Misc(a1)

    adda.w  #Object_Size, a1
    move.l  a1, (RAM_LastObject)
@NoNewPulse:

;----------------;
; Update counter ;
;----------------;

    move.l  d1, Object_Misc(a0)

;-------------------;
; End of subroutine ;
;-------------------;

    rts

;----------------------------------------------------------------------------
