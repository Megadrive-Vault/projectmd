;----------------------------------------------------------------------------
; Project MD
; Copyright 2012 Javier Degirolmo
;
; This file is part of Project MD.
;
; Project MD is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; Project MD is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Project MD.  If not, see <http://www.gnu.org/licenses/>.
;----------------------------------------------------------------------------

;----------------------------------------------------------------------------

;-------------------------------;
; DrawCapacitorTop              ;
;                               ;
; Draws capacitors that go boom ;
;-------------------------------;

DrawCapacitorTop:

;-------------------------------------;
; Check if we're visible for starters ;
;-------------------------------------;

    move.w  Object_X(a0), d0
    sub.w   (RAM_Scroll), d0

    cmp.w   #-32, d0
    ble.s   @End
    cmp.w   #320, d0
    bge.s   @End

;------------;
; Exploding? ;
;------------;

    tst.w   Object_Misc(a0)
    beq.s   @NotExploding

    move.w  Object_X(a0), d0
    move.w  Object_Y(a0), d1
    add.w   #128, d0
    add.w   #128, d1
    sub.w   (RAM_Scroll), d0
    move.w  #$8000|VRAM_CapacExplode, d2
    moveq   #%1111, d3
    bsr     AddSprite

    bra.s   @End

;--------------------;
; Show normal sprite ;
;--------------------;

@NotExploding:
    move.w  Object_X(a0), d0
    move.w  Object_Y(a0), d1
    add.w   #128+4, d0
    add.w   #128+24, d1
    sub.w   (RAM_Scroll), d0
    move.w  #$A000|VRAM_CapacitorTop, d2
    moveq   #%1000, d3
    bsr     AddSprite

;-------------------;
; End of subroutine ;
;-------------------;

@End:
    rts

;----------------------------------------------------------------------------
