;----------------------------------------------------------------------------
; Project MD
; Copyright 2012 Javier Degirolmo
;
; This file is part of Project MD.
;
; Project MD is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; Project MD is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Project MD.  If not, see <http://www.gnu.org/licenses/>.
;----------------------------------------------------------------------------

;----------------------------------------------------------------------------

;------------------------------------------;
; RunFlamethrower                          ;
;                                          ;
; Game logic code for flamethrower objects ;
; This one is for upwards flames           ;
;------------------------------------------;

RunFlamethrower:

;-------------------;
; Are we outbounds? ;
;-------------------;

    move.w  Object_X(a0), d0
    sub.w   (RAM_Scroll), d0
    cmp.w   #0-$40, d0
    blt.s   @Outbounds
    cmp.w   #320+$40, d0
    ble.s   @NotOutbounds
@Outbounds:
    bra     DeleteObject
@NotOutbounds:

;-------------------------------;
; Check if we should throw fire ;
;-------------------------------;

    move.l  (RAM_GlobalAnimSpd), d0
    move.l  Object_Misc(a0), d1
    move.l  d1, d2
    add.l   d0, d1
    cmp.l   #$80<<16, d1
    bgt.s   @DontShoot

    move.l  d1, d3
    and.l   #$3FFFF, d2
    and.l   #$3FFFF, d3
    cmp.l   d3, d2
    ble.s   @DontShoot

;---------------;
; Make fireball ;
;---------------;

    moveq   #0, d0
    btst.l  #2+16, d1
    beq.s   @NoAltDir
    bset.l  #31, d0
@NoAltDir:

    move.w  #Obj_Flameball, Object_Type(a1)
    move.l  Object_X(a0), Object_X(a1)
    move.l  Object_Y(a0), Object_Y(a1)
    move.l  d0, Object_Misc(a1)

    adda.w  #Object_Size, a1
    move.l  a1, (RAM_LastObject)

;-------------------;
; End of subroutine ;
;-------------------;

@DontShoot:
    and.l   #$FFFFFF, d1
    move.l  d1, Object_Misc(a0)
    rts

;----------------------------------------------------------------------------
