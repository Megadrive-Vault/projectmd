;----------------------------------------------------------------------------
; Project MD
; Copyright 2012 Javier Degirolmo
;
; This file is part of Project MD.
;
; Project MD is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; Project MD is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Project MD.  If not, see <http://www.gnu.org/licenses/>.
;----------------------------------------------------------------------------

;----------------------------------------------------------------------------

;---------------------------------------;
; RunFlameball                          ;
;                                       ;
; Game logic code for flameball objects ;
; This is for upwards flames            ;
;---------------------------------------;

RunFlameball:

;-------------------;
; Are we outbounds? ;
;-------------------;

    move.w  Object_X(a0), d0
    sub.w   (RAM_Scroll), d0
    cmp.w   #0-$80, d0
    blt.s   @Outbounds
    cmp.w   #320+$80, d0
    ble.s   @NotOutbounds

@Outbounds:
    bra     DeleteObjectPermanent

;--------------;
; Move upwards ;
;--------------;

@NotOutbounds:
    move.l  (RAM_GlobalAnimSpd), d0
    add.l   d0, d0
    sub.l   d0, Object_Y(a0)

;---------------;
; Went too far? ;
;---------------;

    move.l  Object_Misc(a0), d2
    add.l   d0, d2

    move.l  d2, d1
    bclr.l  #31, d1
    cmp.l   #80<<16, d1
    blt.s   @NotTooFar
    bra     DeleteObjectPermanent

@NotTooFar:
    move.l  d2, Object_Misc(a0)

;---------------;
; Move sideways ;
;---------------;

    asr.l   #4, d0
    btst.l  #31, d2
    beq.s   @NoAltDir
    neg.l   d0
@NoAltDir:
    sub.l   d0, Object_X(a0)

;-----------------------------------;
; Are we colliding with the player? ;
;-----------------------------------;

    move.w  Object_X(a0), d0
    move.w  Player_X(a3), d1
    addq.w  #4, d0
    addq.w  #3, d1
    cmp.w   d0, d1
    blt.s   @End

    add.w   #23, d0
    subq.w  #7, d1
    cmp.w   d0, d1
    bgt.s   @End

    move.w  Object_Y(a0), d0
    move.w  Player_Y(a3), d1
    addq.w  #4, d0
    cmp.w   d0, d1
    blt.s   @End

    add.w   #23, d0
    sub.w   #31, d1
    cmp.w   d0, d1
    bgt.s   @End

;------------------;
; Hurt the player! ;
;------------------;

    bsr     HurtPlayer

;-------------------;
; End of subroutine ;
;-------------------;

@End:
    rts

;----------------------------------------------------------------------------
