;----------------------------------------------------------------------------
; Project MD
; Copyright 2012 Javier Degirolmo
;
; This file is part of Project MD.
;
; Project MD is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; Project MD is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Project MD.  If not, see <http://www.gnu.org/licenses/>.
;----------------------------------------------------------------------------

;----------------------------------------------------------------------------

;----------------------------;
; DrawEndButton              ;
;                            ;
; Draws a goal button object ;
;----------------------------;

DrawEndButton:

;----------------------------------------;
; Check if we're visible for starters    ;
; Spurious sprites cause sprite overflow ;
;----------------------------------------;

    move.w  Object_X(a0), d0
    sub.w   (RAM_Scroll), d0

    cmp.w   #-64, d0
    ble.s   @End
    cmp.w   #320, d0
    bge.s   @End

;------------------------------;
; Calculate sprite coordinates ;
;------------------------------;

    move.w  Object_Y(a0), d1
    add.w   #128, d0
    add.w   #128, d1

;------------------------;
; Determine button color ;
;------------------------;

    move.w  #$8800|(VRAM_EndButton+8), d4
    move.l  Object_Misc(a0), d2
    beq.s   @NotGreen
    move.w  #$8800|(VRAM_EndButton+12), d4
@NotGreen:

;--------;
; Blink! ;
;--------;

    move.w  #$8000|VRAM_EndButton, d2

    btst.b  #GameStat_Blink, (RAM_GameStatus+1)
    bne.s   @DontBlink
    addq.w  #4, d2
    addq.w  #2, d4
@DontBlink:

;-----------------------;
; Put sprites on screen ;
;-----------------------;

    add.w   #24, d1
    moveq   #%1100, d3
    bsr     AddSprite
    bset.l  #11, d2
    add.w   #32, d0
    bsr     AddSprite

    subq.w  #8, d1
    move.w  d4, d2
    moveq   #%0100, d3
    bsr     AddSprite
    sub.w   #16, d0
    bclr.l  #11, d2
    bsr     AddSprite

;-------------------;
; End of subroutine ;
;-------------------;

@End:
    rts

;----------------------------------------------------------------------------
