;----------------------------------------------------------------------------
; Project MD
; Copyright 2012 Javier Degirolmo
;
; This file is part of Project MD.
;
; Project MD is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; Project MD is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Project MD.  If not, see <http://www.gnu.org/licenses/>.
;----------------------------------------------------------------------------

;----------------------------------------------------------------------------

;------------------------------------------------------------------------;
; WaitConsole                                                            ;
;                                                                        ;
; Used to do the delays in the login screen (because Stephany needs time ;
; to react!). Also updates the cursor blinking and such.                 ;
;------------------------------------------------------------------------;
; OUTPUT:                                                                ;
;   d0.w ... 0 to continue as usual                                      ;
;            1 to quit the intro                                         ;
;------------------------------------------------------------------------;
; BREAKS: all                                                            ;
;------------------------------------------------------------------------;

WaitConsole:

;------------------------------------------------------------------------;
; We need to wait for 1.5 seconds, but the amount of frames is different ;
; for NTSC and PAL. Let's figure it out!                                 ;
;------------------------------------------------------------------------;

    moveq   #90-1, d4
    move.w  ($C00004), d1
    btst.l  #0, d1
    beq.s   @SpeedNTSC
    moveq   #75-1, d4
@SpeedNTSC:

;---------------;
; Do the delay! ;
;---------------;

@Delay:

;------------------------;
; Update cursor blinking ;
;------------------------;

    move.b  (RAM_ConBlinkTime), d0
    subq.b  #1, d0
    bne.s   @NoBlink

    move.b  (RAM_ConBlinkSpd), d0
    bchg.b  #GameStat_Blink, (RAM_GameStatus+1)

@NoBlink:
    move.b  d0, (RAM_ConBlinkTime)

;-----------------------;
; Show cursor if needed ;
;-----------------------;

    bsr     ClearSprites
    btst.b  #GameStat_Blink, (RAM_GameStatus+1)
    beq.s   @NoCursor

    moveq   #0, d0
    move.b  (RAM_ConCursorX), d0
    add.w   d0, d0
    add.w   d0, d0
    add.w   d0, d0
    add.w   #128, d0
    move.w  #128+(26*8), d1
    moveq   #$28, d2
    moveq   #%0000, d3
    bsr     AddSprite
@NoCursor:

;---------------------;
; Update video output ;
;---------------------;

    bsr     UpdateSprites
    bsr     VSync
    bsr     FlushVDPBuffer

;---------------------------------;
; Check if the user wants to quit ;
;---------------------------------;

    move.w  d4, -(sp)
    bsr     ReadJoypad
    move.w  (sp)+, d4

    move.b  (RAM_JoypadPress), d1
    and.b   #$F0, d1
    bne.s   @Quit

;------------;
; Next frame ;
;------------;

    dbf     d4, @Delay

;-------------------;
; End of subroutine ;
;-------------------;

    moveq   #0, d0
    rts

@Quit:
    moveq   #1, d0
    rts

;----------------------------------------------------------------------------
