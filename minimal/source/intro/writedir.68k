;----------------------------------------------------------------------------
; Project MD
; Copyright 2012 Javier Degirolmo
;
; This file is part of Project MD.
;
; Project MD is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; Project MD is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Project MD.  If not, see <http://www.gnu.org/licenses/>.
;----------------------------------------------------------------------------

;----------------------------------------------------------------------------

;-----------------------------------------------------------------;
; WriteConsoleDirect                                              ;
;                                                                 ;
; Writes a string in the text mode in the game intro              ;
; This is used to write a string directly at a specified location ;
;-----------------------------------------------------------------;
; INPUT:                                                          ;
;   d0.w ... X coordinate (column)                                ;
;   d1.w ... Y coordinate (row)                                   ;
;   a0.l ... Pointer to string to write                           ;
;-----------------------------------------------------------------;
; BREAKS: all                                                     ;
;-----------------------------------------------------------------;

WriteConsoleDirect:

;----------------------------------;
; Calculate address where to write ;
;----------------------------------;

    lsl.w   #6, d1
    add.w   d1, d0
    add.w   d0, d0
    andi.l  #$3FFF, d0
    swap    d0
    or.l    #$40000003, d0
    move.l  d0, ($C00004)

;-----------------------------------;
; Some addresses we'll access often ;
;-----------------------------------;

    lea     (ASCII2Console), a5
    lea     ($C00000), a6

;-----------------------------------;
; Write the string into the tilemap ;
;-----------------------------------;

@Loop:
    move.b  (a0)+, d7
    beq.s   @End

    andi.w  #$FF, d7
    sub.w   #$20, d7
    add.w   d7, d7
    move.w  (a5,d7.w), (a6)

    bra.s   @Loop

;-------------------;
; End of subroutine ;
;-------------------;

@End:
    rts

;----------------------------------------------------------------------------
