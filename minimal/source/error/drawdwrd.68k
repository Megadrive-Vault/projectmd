;----------------------------------------------------------------------------
; Project MD
; Copyright 2012 Javier Degirolmo
;
; This file is part of Project MD.
;
; Project MD is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; Project MD is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Project MD.  If not, see <http://www.gnu.org/licenses/>.
;----------------------------------------------------------------------------

;----------------------------------------------------------------------------

;-------------------------------------;
; ErrorDrawDword                      ;
;                                     ;
; Draws a dword in the error screen   ;
;-------------------------------------;
; INPUT                               ;
;   d0.w ... X coordinate (in tiles)  ;
;   d1.w ... Y coordinate (in tiles)  ;
;   d2.l ... Value to write on screen ;
;-------------------------------------;
; BREAKS: d0, d1, d2                  ;
;-------------------------------------;

ErrorDrawDword:

;--------------------------;
; Determine where to write ;
;--------------------------;

    lsl.w   #6, d1
    add.w   d1, d0
    add.w   d0, d0
    and.l   #$FFFF, d0
    swap    d0
    or.l    #$60000003, d0
    move.l  d0, (a0)

;-------------------------;
; Loop through all digits ;
;-------------------------;

    moveq   #8-1, d0
@Loop:

;------------------------;
; Get character to write ;
;------------------------;

    rol.l   #4, d2
    move.w  d2, d1
    and.w   #$000F, d1
    addq.w  #1, d1
    move.w  d1, (a1)

;------------;
; Next digit ;
;------------;

    dbf     d0, @Loop

;-------------------;
; End of subroutine ;
;-------------------;

    rts

;----------------------------------------------------------------------------
