;----------------------------------------------------------------------------
; Project MD
; Copyright 2012 Javier Degirolmo
;
; This file is part of Project MD.
;
; Project MD is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; Project MD is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Project MD.  If not, see <http://www.gnu.org/licenses/>.
;----------------------------------------------------------------------------

;----------------------------------------------------------------------------

;-----------------------------------;
; DrawTallyStirng                   ;
;                                   ;
; Draws a string in the score tally ;
;-----------------------------------;
; INPUT                             ;
;   d0.w ... X coordinate           ;
;   d1.w ... Y coorindate           ;
;   a0.l ... String to print        ;
;-----------------------------------;
; BREAKS: all but a4 and a5         ;
;-----------------------------------;

DrawTallyString:

;-----------------;
; Draw first line ;
;-----------------;

    move.l  a0, a1
    moveq   #0, d2
    move.w  d1, d2
    lsl.w   #6, d2
    add.w   d0, d2
    add.w   d2, d2
    swap    d2
    or.l    #$40000003, d2
    move.l  d2, (a4)
    moveq   #0, d2
    bsr     @DoIt

;------------------;
; Draw second line ;
;------------------;

    move.l  a1, a0
    addq.w  #1, d1
    moveq   #0, d2
    move.w  d1, d2
    lsl.w   #6, d2
    add.w   d0, d2
    add.w   d2, d2
    swap    d2
    or.l    #$40000003, d2
    move.l  d2, (a4)
    moveq   #1, d2

;---------------------------------;
; Actual code to draw the strings ;
;---------------------------------;

@DoIt:

;---------------;
; Get character ;
;---------------;

    move.w  (a0)+, d3
    blt.s   @End

;--------;
; Space? ;
;--------;

    bne.s   @NotSpace
    move.l  #0, (a5)
    bra.s   @DoIt
@NotSpace:

;-------------------------;
; Put character on screen ;
;-------------------------;

    add.w   d2, d3
    move.w  d3, (a5)
    addq.w  #2, d3
    move.w  d3, (a5)

;-----------------;
; Next character! ;
;-----------------;

    bra.s   @DoIt

;-------------------;
; End of subroutine ;
;-------------------;

@End:
    rts

;----------------------------------------------------------------------------
